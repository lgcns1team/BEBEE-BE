// Gradle 빌드 스크립트 자체 실행될 필요한 종속성을 로드한다.
buildscript {
    dependencies {
        classpath 'org.flywaydb:flyway-mysql:11.18.0'
        classpath 'com.mysql:mysql-connector-j:9.5.0'
    }
}

plugins {
    // flyway 플러그인
    id "org.flywaydb.flyway" version "11.18.0"
}

dependencies {
    implementation project(":common")

    // Spring Security & JWT
//    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-crypto'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Bean Validation
//    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // MySQL Connector - MySQL 데이터베이스 연결
    runtimeOnly 'com.mysql:mysql-connector-j:9.5.0'

    // QueryDSL - Q-클래스 생성 및 JPA 환경 인식을 위한 Annotation Processor 설정
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // flyway - DB migration tool, DB 스키 관리를 자동화하 체계적으로 수행할 수 있게 도와준다.
    implementation 'org.flywaydb:flyway-core:11.18.0'
    implementation 'org.flywaydb:flyway-mysql:11.18.0'

    // EXIF 메타데이터 추출
    implementation 'com.drewnoakes:metadata-extractor:2.19.0'

    // WebClient (비동기 HTTP 클라이언트) - OCR 서비스 통신용
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // Redis - 리프레시 토큰 블랙리스트 관리용
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Spring Boot Test - 테스트용 종속성
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // TestContainers - Integration Test용 컨테이너
    testImplementation 'org.testcontainers:testcontainers:1.19.3'
    testImplementation 'org.testcontainers:junit-jupiter:1.19.3'
    testImplementation 'org.testcontainers:mysql:1.19.3'
}



// Querydsl Q-Class 생성 경로
def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile
// common 모듈 Q-Class 경로
def commonQuerydslDir = project(':common').file('build/generated/querydsl')

// sourceSets 블록을 수정하, 현재 모듈의 경로 common 모듈의 경로 모두 포함한다.
sourceSets {
    main {
        java {
            srcDirs += querydslDir
            srcDirs += commonQuerydslDir
        }
    }
}

// Q-Class 생성 위치 Annotation Processor의 Output으로 지정한다.
tasks.named('compileJava') {
    options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)
}

// Clean 태스크 정의 - QClass 삭제
clean {
    delete file(querydslDir)
    delete commonQuerydslDir
}

// flyway 설정 정보
flyway {
    url = "jdbc:mysql://${System.getenv('DB_HOST') ?: '127.0.0.1'}:${System.getenv('DB_PORT') ?: '3306'}/${System.getenv('MEMBER_DB_NAME') ?: 'bebee_member'}?serverTimezone=UTC&allowPublicKeyRetrieval=true&useSSL=false&autoReconnect=true&useUnicode=true&characterEncoding=UTF-8"
    user = System.getenv('DB_USERNAME') ?: 'bebee'
    password = System.getenv('DB_PASSWORD') ?: 'bebee1234'
    cleanDisabled = false
}
