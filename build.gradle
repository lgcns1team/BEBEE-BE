/*
* 빌드 시스템의 기능을 확장하고 사용자 정의하는 블럭
* apply false: 루트 프로젝트 서브 프로젝트에 자동으로 적용하지 않는다. -> 단순히 버전 관리
*/
plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.8' apply false
	id 'io.spring.dependency-management' version '1.1.7' apply false
}

/*
* 루트 프로젝트 자체와 그 아래에 포함 모든 서브 프로젝트에 동일하게 설정하는 블럭
*/
allprojects {
	group = 'com.lgcns.bebee'
	version = '1.0.0'
	description = 'A sandbox environment for validating and testing technology stacks for the "bebee" project in the LGCNS bootcamp'

	repositories{
		mavenCentral()
	}
}

/*
* 현재 빌드 스크립트가 속한 프로젝트의 모든 하위 프로젝트 모듈에만 적용되는 설정 블럭
*/
subprojects {
	// 서브 프로젝트 플러그인 활성화
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	java {
		// Java Toolchain 설정: Gradle 빌드 환경 Java 버전 명시적으로 지정
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}

	dependencies{
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testCompileOnly 'org.projectlombok:lombok'
		testAnnotationProcessor 'org.projectlombok:lombok'
	}

	test {
		useJUnitPlatform()
	}
}
