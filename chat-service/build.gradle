dependencies {
	implementation project(":common")

	// MySQL Connector - MySQL 데이터베이스 연결
	runtimeOnly 'com.mysql:mysql-connector-j'

	// Spring Data Mongodb - Mongodb Client
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'

	// QueryDSL - Q-클래스 생성 및 JPA 환경 인식을 위한 Annotation Processor 설정
	annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
	annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

	// Spring Actuator - 어플리케이 모니터링 및 관리 엔드포인트를 제공
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// flyway - DB migration tool, DB 스키 관리를 자동화하 체계적으로 수행할 수 있게 도와준다.
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-mysql'
}

// Querydsl Q-Class 생성 경로
def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile
// common 모듈 Q-Class 경로
def commonQuerydslDir = project(':common').file('build/generated/querydsl')

// sourceSets 블록을 수정하, 현재 모듈의 경로 common 모듈의 경로 모두 포함한다.
sourceSets {
	main {
		java {
			srcDirs += querydslDir
			srcDirs += commonQuerydslDir
		}
	}
}

// Q-Class 생성 위치 Annotation Processor의 Output으로 지정한다.
tasks.named('compileJava') {
	options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)
}

// Clean 태스크 정의 - QClass 삭제
clean {
	delete file(querydslDir)
	delete commonQuerydslDir
}