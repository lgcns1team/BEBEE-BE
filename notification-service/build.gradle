// Gradle 빌드 스크립트 자체 실행될 필요한 종속성을 로드한다.
buildscript {
	dependencies {
		classpath 'org.flywaydb:flyway-mysql:11.18.0'
		classpath 'com.mysql:mysql-connector-j:9.5.0'
	}
}

plugins {
	// flyway 플러그인
	id "org.flywaydb.flyway" version "11.18.0"
}

dependencies {
	implementation project(":common")
	implementation project(":common-data")

	// MySQL Connector - MySQL 데이터베이스 연결
	runtimeOnly 'com.mysql:mysql-connector-j:9.5.0'

	// QueryDSL - Q-클래스 생성 및 JPA 환경 인식을 위한 Annotation Processor 설정
	annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
	annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

	// flyway - DB migration tool, DB 스키마 관리를 자동화하여 체계적으로 수행할 수 있게 도와준다.
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-mysql'

	// firebase admin sdk - FCM 웹 푸시 메시지 전송
	implementation 'com.google.firebase:firebase-admin:9.7.0'
}

// Querydsl Q-Class 생성 경로
def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile
// common-data 모듈 Q-Class 경로
def commonDataQuerydslDir = project(':common-data').file('build/generated/querydsl')

// sourceSets 블록을 수정하여 현재 모듈의 경로와 common-data 모듈의 경로 모두 포함한다.
sourceSets {
	main {
		java {
			srcDirs += querydslDir
			srcDirs += commonDataQuerydslDir
		}
	}
}

// Q-Class 생성 위치 Annotation Processor의 Output으로 지정한다.
tasks.named('compileJava') {
	options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)
}

// Clean 태스크 정의 - QClass 삭제
clean {
	delete file(querydslDir)
	delete commonDataQuerydslDir
}

// flyway 설정 정보
flyway {
	url = "jdbc:mysql://${System.getenv('DB_HOST') ?: '127.0.0.1'}:${System.getenv('DB_PORT') ?: '3306'}/${System.getenv('NOTIFICATION_DB_NAME') ?: 'bebee_notification'}?serverTimezone=UTC&allowPublicKeyRetrieval=true&useSSL=false&autoReconnect=true&useUnicode=true&characterEncoding=UTF-8"
	user = System.getenv('DB_USERNAME') ?: 'bebee'
	password = System.getenv('DB_PASSWORD') ?: 'bebee1234'
	cleanDisabled = false
}